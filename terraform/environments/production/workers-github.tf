# =============================================================================
# GitHub Bot Worker
# =============================================================================

resource "cloudflare_queue" "github_autofix" {
  count = var.enable_github_bot ? 1 : 0

  account_id = var.cloudflare_account_id
  queue_name = "open-inspect-github-autofix-${local.name_suffix}"
}

resource "cloudflare_queue" "github_autofix_dlq" {
  count = var.enable_github_bot ? 1 : 0

  account_id = var.cloudflare_account_id
  queue_name = "open-inspect-github-autofix-dlq-${local.name_suffix}"
}

# Build github-bot worker bundle (only runs during apply, not plan)
resource "null_resource" "github_bot_build" {
  count = var.enable_github_bot ? 1 : 0

  triggers = {
    always_run = timestamp()
  }

  provisioner "local-exec" {
    command     = "npm run build"
    working_dir = "${var.project_root}/packages/github-bot"
  }
}

module "github_bot_worker" {
  count  = var.enable_github_bot ? 1 : 0
  source = "../../modules/cloudflare-worker"

  account_id       = var.cloudflare_account_id
  worker_name      = "open-inspect-github-bot-${local.name_suffix}"
  worker_subdomain = var.cloudflare_worker_subdomain
  script_path      = local.github_bot_script_path

  kv_namespaces = [
    {
      binding_name = "GITHUB_KV"
      namespace_id = module.github_kv[0].namespace_id
    }
  ]

  service_bindings = [
    {
      binding_name = "CONTROL_PLANE"
      service_name = "open-inspect-control-plane-${local.name_suffix}"
    }
  ]

  enable_service_bindings = var.enable_service_bindings

  queue_bindings = [
    {
      binding_name = "AUTOFIX_QUEUE"
      queue_name   = cloudflare_queue.github_autofix[0].queue_name
    }
  ]

  plain_text_bindings = [
    { name = "DEPLOYMENT_NAME", value = var.deployment_name },
    { name = "APP_NAME", value = var.app_name },
    { name = "DEFAULT_MODEL", value = "openai/gpt-5.6-sol" },
    { name = "DEFAULT_REASONING_EFFORT", value = "xhigh" },
    { name = "GITHUB_BOT_USERNAME", value = var.github_bot_username },
  ]

  secrets = [
    { name = "GITHUB_APP_ID", value = var.github_app_id },
    { name = "GITHUB_APP_PRIVATE_KEY", value = var.github_app_private_key },
    { name = "GITHUB_APP_INSTALLATION_ID", value = var.github_app_installation_id },
    { name = "GITHUB_WEBHOOK_SECRET", value = var.github_webhook_secret },
    { name = "SERVICE_AUTH_SECRET", value = random_password.service_auth_secret_github_bot.result },
  ]

  compatibility_date  = "2024-09-23"
  compatibility_flags = ["nodejs_compat"]

  depends_on = [null_resource.github_bot_build[0], module.control_plane_worker, module.github_kv[0]]
}

resource "cloudflare_queue_consumer" "github_autofix" {
  count = var.enable_github_bot ? 1 : 0

  account_id        = var.cloudflare_account_id
  queue_id          = cloudflare_queue.github_autofix[0].queue_id
  type              = "worker"
  script_name       = module.control_plane_worker.worker_name
  dead_letter_queue = cloudflare_queue.github_autofix_dlq[0].queue_name
  settings = {
    batch_size       = 1
    max_wait_time_ms = 1000
    max_concurrency  = 5
    max_retries      = 4
    retry_delay      = 30
  }

  depends_on = [module.github_bot_worker, module.control_plane_worker]
}
